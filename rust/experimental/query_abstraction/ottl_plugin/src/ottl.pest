// These two special rules, when defined, are implicitly allowed at:
// - at every sequence (split by ~)
// - between every repetition (+ or *)
// Atomics (marked with @) are excluded
// See https://pest.rs/book/grammars/syntax.html#implicit-whitespace
WHITESPACE = _{ " " }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Basic tokens
// ----------------------------------------------
true_token = @{ "true" }
false_token = @{ "false" }
integer_token = @{ 
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
}
string_token = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
equals_token = @{ "==" }
not_equals_token = @{ "!=" }
comma_token = @{ "," }
and_token = @{ "and" }
or_token = @{ "or" }
greater_than_token = @{ ">" ~ !"=" }
less_than_token = @{ "<" ~ !"=" }
greater_than_or_equal_to_token = @{ ">=" }
less_than_or_equal_to_token = @{ "<=" }
open_paren_token = @{ "(" }
close_paren_token = @{ ")" }
not_token = @{ "not" }
where_token = @{ "where" }
// ----------------------------------------------

// Basic Grammar
// ----------------------------------------------
literal = { true_token | false_token | integer_token | string_token }
identifier = @{ ("_" | ASCII_ALPHANUMERIC)+ }

expression_base = { identifier | literal | enclosed_expression }

enclosed_expression = { open_paren_token ~ expression ~ close_paren_token }

and_expression = { expression_base ~ (and_token ~ expression)+ }
or_expression = { expression_base ~  (or_token ~ expression)+ }

equals_expression = { expression_base ~ equals_token ~ expression }
not_equals_expression = { expression_base ~ not_equals_token ~ expression }
greater_than_expression = { expression_base ~ greater_than_token ~ expression }
less_than_expression = { expression_base ~ less_than_token ~ expression }
greater_than_or_equal_to_expression = { expression_base ~ greater_than_or_equal_to_token ~ expression }
less_than_or_equal_to_expression = { expression_base ~ less_than_or_equal_to_token ~ expression }

binary_logical_expression = {
    and_expression
    | or_expression
}
comparison_expression = {
    equals_expression
    | not_equals_expression
    | greater_than_expression
    | less_than_expression
    | greater_than_or_equal_to_expression
    | less_than_or_equal_to_expression
}
negated_expression = { not_token ~ enclosed_expression }

predicate = {
    binary_logical_expression
    | comparison_expression
    | negated_expression
}

expression = { 
    predicate
    | enclosed_expression
    | literal
    | identifier 
}
// ----------------------------------------------

// OTTL-specific Tokens
// ----------------------------------------------
dash_token = @{ "-" ~ !"-" }
single_quote_token = @{ "'" }
filter_token = @{ "filter:" }
transform_token = @{ "transform:" }
logs_token = @{ "logs:" }
log_record_token = @{ "log_record:" }
log_statements_token = @{ "log_statements:" }
set_token = @{ "set" }
// ----------------------------------------------

// OTTL-specific Grammar
// ----------------------------------------------
set_tuple = { identifier ~ comma_token ~ expression }
set_with_paren = { set_token ~ open_paren_token ~ set_tuple ~ close_paren_token }

filter_statement = { dash_token ~ single_quote_token ~ predicate ~ single_quote_token }
extend_statement = { dash_token ~ set_with_paren ~ (where_token ~ predicate)? }

filter_query = {
    filter_token ~ NEWLINE? 
    ~ logs_token ~ NEWLINE? 
    ~ log_record_token ~ NEWLINE? 
    ~ (NEWLINE* ~ filter_statement)+
}

transform_query = {
    transform_token ~ NEWLINE?
    ~ log_statements_token ~ NEWLINE?
    ~ (NEWLINE* ~ extend_statement)+
}

query = { 
    SOI ~ filter_query ~ NEWLINE* ~ EOI
    | SOI ~ transform_query ~ NEWLINE* ~ EOI
}
// ----------------------------------------------